/*******************************************************************************
 * Copyright (c) 2011 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.graphview.style.GraphViewStyle with org.eclipse.xtext.xbase.Xbase

import "http://www.xtext.org/graphview/style/GraphViewStyle"
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/graphview/map/GraphViewMapping" as gvmapping
import "http://www.eclipse.org/emf/2002/Ecore"

StyleSheet:
	importSection=XImportSection
	'stylesheet' name=QualifiedName 'for' diagramMapping=[gvmapping::DiagramMapping|QualifiedName]
	styles+=Style*;
	
Style:
	'style' mappings+=[gvmapping::AbstractMapping|QualifiedName] (',' mappings+=[gvmapping::AbstractMapping|QualifiedName])* 
	('as' javaClass=JvmTypeReference)?
	expression=XBlockExpression?;

XLiteral returns xbase::XExpression:
	XClosure |
	XBooleanLiteral |
	XNumberLiteral |
	XNullLiteral |
	XStringLiteral |
	XTypeLiteral |
	XColorLiteral;

XColorLiteral:
	color=COLOR;

terminal COLOR:
	'#' ('0'..'9'|'a'..'f'|'A'..'F')
	('0'..'9'|'a'..'f'|'A'..'F')
	('0'..'9'|'a'..'f'|'A'..'F')
	('0'..'9'|'a'..'f'|'A'..'F')
	('0'..'9'|'a'..'f'|'A'..'F')
	('0'..'9'|'a'..'f'|'A'..'F');
