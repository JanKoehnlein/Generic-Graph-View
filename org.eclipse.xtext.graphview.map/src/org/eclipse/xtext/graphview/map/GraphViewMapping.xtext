grammar org.eclipse.xtext.graphview.map.GraphViewMapping with org.eclipse.xtext.xbase.Xbase

import "platform:/resource/org.eclipse.xtext.graphview.map/src/org/eclipse/xtext/graphview/map/GraphViewMapping.ecore"
import "http://www.eclipse.org/xtext/xbase/Xbase" as xbase
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as jvmTypes
import "http://www.eclipse.org/emf/2002/Ecore"

DiagramMapping:
	imports+=Import*
	'diagram' name=QualifiedName 'type' typeGuard=JvmTypeReference '{'
		mappings+=AbstractExpressionMapping*
	'}';

Import:
	'import' importedNamespace=QualifiedNameWithWildcard;

QualifiedNameWithWildcard:
	QualifiedName ('.' '*')?;

AbstractExpressionMapping:
	NodeMapping | LabelMapping | EdgeMapping | MappingCall;
	
NodeMapping:
	'node' name=ID 'for' multi?='each'? expression=XExpression '{'
		mappings+=AbstractExpressionMapping*
	'}';

LabelMapping:
	'label' name=ID 'for' multi?='each'? expression=XExpression;

EdgeMapping:
	'edge' name=ID 'for' multi?='each'? expression=XExpression '{'
		sourceMapping=EdgeEndMapping? 
		'->' 
		targetMapping=EdgeEndMapping? 
		mappings+=LabelMapping*
	'}';
	
EdgeEndMapping:
	('ref' | call?='call') referencedMapping=[AbstractMappingDefinition] 'for' expression=XExpression;
	
MappingCall:
	'call' referencedMapping=[AbstractMappingDefinition] 'for' multi?='each'? expression=XExpression;

