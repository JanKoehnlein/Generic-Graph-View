/*******************************************************************************
 * Copyright (c) 2011 itemis AG (http://www.itemis.eu) and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *******************************************************************************/
grammar org.eclipse.xtext.graphview.map.GraphViewMapping with org.eclipse.xtext.xbase.Xbase

import "http://www.eclipse.org/xtext/graphview/map/GraphViewMapping"
import "http://www.eclipse.org/emf/2002/Ecore"

DiagramMapping:
	imports+=Import*
	'diagram' name=QualifiedName 'type' typeGuard=JvmTypeReference '{'
		mappings+=AbstractExpressionMapping*
	'}' ('unless' unlessCondition=XExpression)?;

Import:
	'import' importedNamespace=QualifiedNameWithWildcard;

QualifiedNameWithWildcard:
	QualifiedName ('.' '*')?;

AbstractExpressionMapping:
	NodeMapping | LabelMapping | EdgeMapping | MappingCall;
	
NodeMapping:
	'node' name=ID 'for' multi?='each'? expression=XExpression '{'
		mappings+=AbstractExpressionMapping*
	'}' ('unless' unlessCondition=XExpression)?;

LabelMapping:
	'label' name=ID 'for' multi?='each'? expression=XExpression ('unless' unlessCondition=XExpression)?;

EdgeMapping:
	'edge' name=ID 'for' multi?='each'? expression=XExpression '{'
		sourceMapping=EdgeEndMapping? 
		'=>' 
		targetMapping=EdgeEndMapping? 
		mappings+=LabelMapping*
	'}' ('unless' unlessCondition=XExpression)?;
	
EdgeEndMapping:
	('ref' | call?='call') referencedMapping=[AbstractMappingDefinition] 'for' expression=XExpression;
	
MappingCall:
	'call' referencedMapping=[AbstractMappingDefinition] 'for' multi?='each'? expression=XExpression ('unless' unlessCondition=XExpression)?;

